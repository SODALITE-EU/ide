/*******************************************************************************
 * Copyright (c) 2019 Atos Spain S.A.
 * This program and the accompanying materials
 * are made available under the terms of the Apache Public License 2.0 (APL2)
 * which accompanies this distribution, and is available at
 * https://apache.org/licenses/LICENSE-2.0
 *
 * Contributors:
 *     Jesús Gorroñogoitia - Design and implementation
 *******************************************************************************/
grammar org.sodalite.dsl.RM with org.eclipse.xtext.common.Terminals

generate rM "http://www.sodalite.org/dsl/RM"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

RM_Model:
	('data_types:'
	BEGIN
	dataTypes=EDataTypes
	END)? &
	('artifact_types:'
	BEGIN
	artifactTypes=EArtifactTypes
	END)? &
	('capability_types:'
	BEGIN
	capabilityTypes=ECapabilityTypes
	END)? &
	('interface_types:'
	BEGIN
	interfaceTypes=EInterfaceTypes
	END)? &
	('relationship_types:'
	BEGIN
	relationshipTypes=ERelationshipTypes
	END)? &
	('node_types:'
	BEGIN
	nodeTypes=ENodeTypes
	END)? &
	('policy_types:'
	BEGIN
	policyTypes=EPolicyTypes
	END)?;

EDataTypes:
	{EDataTypes} (dataTypes+=EDataType)+;

EDataType:
	name=EDataTypeName ':'
	BEGIN
	data=EDataTypeBody
	END;

EDataTypeBody:
	{EDataTypeBody} (('derived_from:' superType=[EDataType|EDataTypeName])? &
	('description:' description=STRING)? &
	('constraints:'
	BEGIN
	constraints=EConstraints
	END)? &
	('properties:'
	BEGIN
	properties=EProperties
	END)?);

EDataTypeName:
	QUALIFIED_NAME | PRIMITIVE_DATA_TYPE;

EArtifactTypes:
	{EArtifactTypes} (artifactTypes+=EArtifactType)+;

EArtifactType:
	name=QUALIFIED_NAME ':'
	BEGIN
	artifact=EArtifactTypeBody
	END;

EArtifactTypeBody:
	{EArtifactTypeBody} (('derived_from:' superType=[EArtifactType|QUALIFIED_NAME])? &
	('description:' description=STRING)? &
	('mime_type:' mime_type=STRING)? &
	('file_ext:'+ file_ext=STRING)?);

ECapabilityTypes:
	{ECapabilityTypes} (capabilityTypes+=ECapabilityType)+;

ECapabilityType:
	name=QUALIFIED_NAME ':'
	BEGIN
	capability=ECapabilityTypeBody
	END;

ECapabilityTypeBody:
	{ECapabilityTypeBody} (('derived_from:' superType=[ECapabilityType|QUALIFIED_NAME])? &
	('description:' description=STRING)? &
	('properties:'
	BEGIN
	properties=EProperties
	END)? &
	('attributes:'
	BEGIN
	atributes=EAttributes
	END)?);

EInterfaceTypes:
	{EInterfaceTypes} (interfaceTypes+=EInterfaceType)+;

EInterfaceType:
	name=QUALIFIED_NAME ':'
	BEGIN
	interface=EInterfaceTypeBody
	END;

EInterfaceTypeBody:
	{EInterfaceTypeBody} (('derived_from:' superType=[EInterfaceType|QUALIFIED_NAME])? &
	('description:' description=STRING)? &
	('inputs:'
	BEGIN
	inputs=EProperties
	END)? &
	('operations:'
	BEGIN
	operations=EOperations
	END)?);

EPolicyTypes:
	{EPolicyTypes} (policyTypes+=EPolicyType)+;

EPolicyType:
	name=QUALIFIED_NAME ':'
	BEGIN
	policy=EPolicyTypeBody
	END;

EPolicyTypeBody:
	{EPolicyTypeBody} (('derived_from:' superType=[EPolicyType|QUALIFIED_NAME])? &
	('description:' description=STRING)?);

ENodeTypes:
	{ENodeTypes} (nodeTypes+=ENodeType)+;

ENodeType:
	name=QUALIFIED_NAME ':'
	BEGIN
	node=ENodeTypeBody
	END;

ENodeTypeBody:
	{ENodeTypeBody} (('derived_from:' superType=[ENodeType|QUALIFIED_NAME])? &
	('description:' description=STRING)? &
	('properties:'
	BEGIN
	properties=EProperties
	END)? &
	('attributes:'
	BEGIN
	attributes=EAttributes
	END)? &
	('interfaces:'
	BEGIN
	interfaces=EInterfaces
	END)? &
	('capabilities:'
	BEGIN
	capabilities=ECapabilities
	END)? &
	('requirements:'
	BEGIN
	requirements=ERequirements
	END)?);

ERelationshipTypes:
	{ERelationshipTypes} (relationshipTypes+=ERelationshipType)+;

ERelationshipType:
	name=QUALIFIED_NAME ':'
	BEGIN
	relationship=ERelationshipTypeBody
	END;

ERelationshipTypeBody:
	{ERelationshipTypeBody} (('derived_from:' superType=[ERelationshipType|QUALIFIED_NAME])? &
	('description:' description=STRING)? &
	('properties:'
	BEGIN
	properties=EProperties
	END)? &
	('attributes:'
	BEGIN
	atributes=EAttributes
	END)? &
	('interfaces:'
	BEGIN
	interfaces=EInterfaces
	END)? &
	('valid_target_types:' '[' valid_target_types=EValidTargetTypes "]")?);

EValidTargetTypes:
	targetTypes+=ECapabilityTypeRef (',' sourceType+=ECapabilityTypeRef)*;

ECapabilityTypeRef:
	name=[ECapabilityType|QUALIFIED_NAME];

EProperties:
	{EProperties} (properties+=EPropertyDefinition)*;

EPropertyDefinition:
	name=ID ':'
	BEGIN
	property=EPropertyDefinitionBody
	END;

EPropertyDefinitionBody:
	('type:' type=[EDataType|EDataTypeName]) &
	('description:' description=STRING)? &
	('required:' required=BOOLEAN)? &
	('default:' default=EValueExpression)? &
	('status:' status=STRING)? &
	('constraints:'
	BEGIN
	constraints=EConstraints
	END)? &
	('entry_schema:' entry_schema=[EDataType|EDataTypeName])?;

EAttributes:
	{EAttributes} (attributes+=EAttributeDefinition)*;

EAttributeDefinition:
	name=ID ':'
	BEGIN
	attribute=EAttributeDefinitionBody
	END;

EAttributeDefinitionBody:
	('type:' type=[EDataType|EDataTypeName]) &
	('description:' description=STRING)? &
	('default:' default=EValueExpression)? &
	('status:' status=STRING)? &
	('entry_schema:' entry_schema=[EDataType|EDataTypeName])?;

EInterfaces:
	{EInterfaces} (interfaces+=EInterfaceDefinition)*;

EInterfaceDefinition:
	name=ID ':'
	BEGIN
	interface=EInterfaceDefinitionBody
	END;

EInterfaceDefinitionBody:
	'type:' type=[EInterfaceType|QUALIFIED_NAME] &
	('inputs:'
	BEGIN
	inputs=EProperties
	END)? &
	('operations:'
	BEGIN
	operations=EOperations
	END)?;

EOperations:
	{EOperations} (operations+=EOperationDefinition)*;

EOperationDefinition:
	name=ID ':'
	BEGIN
	operation=EOperationDefinitionBody
	END;

EOperationDefinitionBody:
	{EOperationDefinitionBody} (
	('description:' description=STRING)? &
	('inputs:'
	BEGIN
	inputs=EInputs
	END)? &
	('implementation:'
		BEGIN
		implementation=EImplementation
		END
	)?
	);
	
EImplementation:
	(primary=EPrimary)
	(dependencies=EDependencies)?
;

EPrimary:
	('primary:' file=STRING)
	(BEGIN 'relative_path:' relative_path=STRING END)?
;

EDependencies:
	'dependencies:''[' (files=EDependencyFiles)? ']'
	(BEGIN 'relative_path:' relative_path=STRING END)?
;

EDependencyFiles:
	files+=STRING (',' files+=STRING)*
;

EInputs:
	{EInputs} (inputs+=EParameterDefinition)*;

	//inputs of operation definition in NodeTypes are ParameterDefinition
EParameterDefinition:
	name=ID ':'
	BEGIN
	parameter=EParameterDefinitionBody
	END;

EParameterDefinitionBody:
	{EParameterDefinitionBody} (
	('type:' type=[EDataType|EDataTypeName])? &
	('value:' value=EValueExpression)? &
	('default:' default=EValueExpression)?);

EValueExpression:
	ESingleValue | EFunction;

EFunction:
	GetProperty | GetAttribute | GetInput;

GetInput:
	'get_input:' input=[EParameterDefinition]
;

GetAttribute:
	'get_attribute:'
	BEGIN
	attribute=GetAttributeBody
	END;

GetAttributeBody:
	('attribute:' attribute=[EAttributeDefinition|QUALIFIED_NAME]) &
	('entity:' entity=ENTITY) &
	('req_cap:' req_cap=[EReqOrCap|QUALIFIED_NAME])?;

GetProperty:
	'get_property:'
	BEGIN
	property=GetPropertyBody
	END;

GetPropertyBody:
	('property:' property=[EPropertyDefinition|QUALIFIED_NAME]) &
	('entity:' entity=ENTITY) &
	('req_cap:' req_cap=[EReqOrCap|QUALIFIED_NAME])?;

ECapabilities:
	{ECapabilities} (capabilities+=ECapabilityDefinition)*;

ECapabilityDefinition:
	name=ID ':'
	BEGIN
	capability=ECapabilityDefinitionBody
	END;

ECapabilityDefinitionBody:
	('type:' type=[ECapabilityType|QUALIFIED_NAME]) &
	('description:' description=STRING)? &
	('properties:'
	BEGIN
	properties=EProperties
	END)? &
	('attributes:'
	BEGIN
	attributes=EAttributes
	END)? &
	('valid_source_types:' '[' (valid_source_types+=EValidSourceType)? ']')? &
	('occurrences:' '[' occurrences_start=EAlphaNumericValue ',' occurrences_end=EAlphaNumericValue ']')?;

EValidSourceType:
	sourceTypes+=ENodeTypeRef (',' sourceType+=ENodeTypeRef)*;

ENodeTypeRef:
	name=[ENodeType|QUALIFIED_NAME];

ERequirements:
	{ERequirements} (requirements+=ERequirementDefinition)*;

ERequirementDefinition:
	name=ID ':'
	BEGIN
	requirement=ERequirementDefinitionBody
	END;

ERequirementDefinitionBody:
	('capability:' capability=[ECapabilityType|QUALIFIED_NAME]) &
	('node:' node=[ENodeType|QUALIFIED_NAME])? &
	('relationship:' relationship=[ERelationshipType|QUALIFIED_NAME])? &
	('occurrences:' '[' start=EAlphaNumericValue ',' end=EAlphaNumericValue ']')?;

EReqOrCap:
	ERequirementDefinition | ECapabilityDefinition;

EConstraints:
	{EConstraints} (constraints+=EConstraint)*;

EConstraint:
	EEqual | EGreaterThan | EGreaterOrEqual | ELessThan | ELessOrEqual | EInRange | EValid_Values | ELength | EMinLength |
	EMaxLength;

EMaxLength:
	'max_length:' val=EAlphaNumericValue;

EMinLength:
	'min_length:' val=EAlphaNumericValue;

ELength:
	'length:' val=EAlphaNumericValue;

EValid_Values:
	'valid_values:' val=ELIST;

ELIST:
	'['list+=EAlphaNumericValue ("," list+=EAlphaNumericValue)* ']'
;

EInRange:
	'in_range:' '[' start=EAlphaNumericValue "," end=EAlphaNumericValue ']';

ELessOrEqual:
	'less_or_equal:' val=EAlphaNumericValue;

ELessThan:
	'less_than:' val=EAlphaNumericValue;

EGreaterOrEqual:
	'greater_or_equal:' val=EAlphaNumericValue;

EGreaterThan:
	'greater_than:' val=EAlphaNumericValue;

EEqual:
	'equal:' val=ESingleValue;
	
EAssignmentValue:
	ESingleValue | ELIST | EMAP | EFunction
;

ESingleValue:
	ESTRING | EBOOLEAN | EFLOAT | ESIGNEDINT 
;

EAlphaNumericValue:
	ESTRING | EFLOAT | ESIGNEDINT 
;

ESTRING:
	value = STRING
;

EBOOLEAN:
	value = BOOLEAN
;

EFLOAT:
	value = FLOAT
;

ESIGNEDINT:
	value = SIGNEDINT
;

EMAP:
	BEGIN
		map+=EMapEntry+
	END
;

EMapEntry:
	key=ID ":" value=EAssignmentValue
;

terminal QUALIFIED_NAME: //FIXME convert to data type rule, remove terminal
	ID ('.' ID)+;

terminal BOOLEAN returns ecore::EBoolean:
	'true' | 'false';
	
SIGNEDINT returns ecore::EInt: '-'? INT;

FLOAT returns ecore::EFloat: INT? '.' INT;

terminal ENTITY:
	'SELF' | 'SOURCE' | 'TARGET' | 'HOST';

terminal PRIMITIVE_DATA_TYPE:
	'string' | 'integer' | 'float'| 'boolean' | 'map' | 'list' | 'range' | 'timestamp' | 'scalar-unit.frequency' |
	'scalar-unit.size' | 'version-type';

@Override
terminal ID:
	'^'? ('a'..'z' | 'A'..'Z' | '_') ('a'..'z' | 'A'..'Z' | '_' | '-' | '0'..'9')*;

//Blocks delimited with curly braces
//terminal START_BLOCK:
//	'{'
//;
//
//terminal END_BLOCK:
//	'}'
//;

//Positional blocks delimited with indentation
terminal BEGIN: 'synthetic:BEGIN';
terminal END: 'synthetic:END';
